;FLOPPY DISK BIOS FUER DIE UDC-KARTE
;NACH ELEKTOR COMPUTING 6

;D. LAUSBERG	(C) 1987
;V1.2	11.11.88
;V1.3	13.12.89	PRINTER ROUTINES
;V1.4	14.06.90	NEW PARAMETERS
;V1.5	14.10.90	IMPROVEMENTS
;V1.6	10.11.91	ASM-65 EXTENDED VERSION
;V2.0	20.11.91	IRQ BASED VERSION
;V2.1	27.05.92	BOOT ROM VERSION
;V2.2	18.06.92	INCREASED SPEED, FORMAT TRACK
;V2.3	09.03.93	DOUBLE DRIVE
;V2.4	21.02.95	new memory map
;V2.5	27.03.95	SCSI support
;V2.6	09.12.95	2 logical SCSI discs
;V2.7	26.07.98	slightly modified memory map
;V2.8	30.06.00	change to sector bios
;V2.9	08.11.02	disk parameters corrected
;V2.A	25.03.21	4 Floppies + 2 HD Drives
;V2.B	30.05.21	SCSI2SD corrections interim fix, I2C commands
;V3.0	07.02.22	BOOTPROM v3.0 version
;V3.1	25.03.23	BOOTPROM v3.2 version
;V3.2	06.07.23	SCSI Write error correction

; change according to your dir structure
#include "../../m65.i"

#include "cpm65.xa65"

VERSION	= $32		;VERSION NUMBER

;CONSTANTS

MAXTRK	= 80		;MAX TRACKS

SPT	= 16		;SECTORS PER TRACK
SPTDIV	= 4		;log(2) SPT
SPB	= 4		;SECTORS PER BLOCK
SPBDIV	= 2		;LOG(2) SPB
SYS	= 32		;SYSTEM sectors
BPD	= (MAXTRK*SPT-SYS)/SPB-1	;BLOCKS PER DISK
DIRSCS	= 16		;DIRECTORY SECTORS 128 entries
DIRMSK	= %11110000	;DIR MASK FOR BAT
RPEXT	= 8*SPB*2 	;RECORD PER EXTENT
BATSIZ	= BPD/8		;SIZE OF BAT

;-----------------------------------------------------------

; disk 4MiB = 256 tracks of 64 sectors/track
; 1 system track and 256 directory entries

HMAXTRK	= 256

HSPT	= 64		;SECTORS PER TRACK
HSPTDIV	= 6		;log(2) SPT
HSPB	= 8		;SECTORS PER BLOCK
HSPBDIV	= 3		;LOG(2) SPB
HSYS	= 64		;SYSTEM sectors
HBPD	= (HMAXTRK*HSPT-HSYS)/HSPB-1	;BLOCKS PER DISK
HDIRSCS	= 32		;DIRECTORY SECTORS 256 entries
HDIRMSK	= %11110000	;DIR MASK FOR BAT
HRPEXT	= 8*HSPB*2 	;RECORD PER EXTENT
HBATSIZ	= HBPD/8	;SIZE OF BAT (64 bytes)

;==========================================

	* = BIOSSTART

FBIOS	JMP BOOT		; 00
	JMP WBOOT		; 01
	JMP CONST		; 02
	JMP CONIN		; 03
	JMP CONOUT		; 04
	JMP LIST		; 05
	JMP AUXOUT		; 06
	JMP AUXIN		; 07
	JMP HOME		; 08
	JMP SELDSK		; 09
	JMP SETTRK		; 0A
	JMP SETSEC		; 0B
	JMP READ		; 0C
	JMP WRITE		; 0D
	JMP LISTST		; 0E
	JMP VERSION		; 0F
	JMP MONITOR		; 10

;========== DATA AREA =============

; disk offset table LBA1 = LBA1 OR DSKOFS(drive)

DSKOFS	.byt %11000000	;320k a
	.byt %11010000	;320k b
	.byt %11100000	;320k c
	.byt %11110000	;320k d
	.byt %00000000	;4M e
	.byt %01000000	;4M f
	.byt %10000000	;4M g

SECTOR	.word 0
	.byt 0

DRIVE	.byt 0

; drive a, b, c, d

DPH320K	.word DIRBF	;DISK PARAMETER HEADER
	.word DMA	;FOR DRIVE A to D
	.word BAT

	.byt SPB-1	;DISK PARAMETER BLOCK FOR DISK A
	.byt SPBDIV
	.word BPD
	.byt DIRSCS
	.byt DIRMSK
	.byt RPEXT
	.byt SYS,0,0
	.byt BATSIZ

; drive e, f, g

DPH4M	.word DIRBF	;Disk Parameter Header for Drive E
	.word DMA
	.word BAT

	.byt HSPB-1	;SPB
	.byt HSPBDIV	;SPBDIV
	.word HBPD	;BPD
	.byt HDIRSCS	;DIRSCSC
	.byt HDIRMSK	;DIRMSK
	.byt HRPEXT	;RPEXT
	.byt HSYS,0,0	;SYS
	.byt HBATSIZ	;BATSIZ

BAT	.byt %11110000	;Block Allocation Table Drive A-F
	.dsb 255,0	;max. 1 page

;==================================

BOOT	LDA #$4C		;SET JMP BDOS
	STA JPBDOS
	LDA #<CCPSTART
	STA JPBDOS+1
	LDA #>CCPSTART
	STA JPBDOS+2
	JSR HOME		;HOME BOOT DISK
	LDA DRIVE		;SELECT BOOT DRIVE
	LDX #$0E		;VIA BDOS
	JSR JPBDOS
	LDX #0 			;WARM BOOT
	JMP JPBDOS

WBOOT	LDA DRIVE		;SELECT DEFAULT DRIVE
	JSR SELDSK
	RTS

CONST	JMP mcont

CONIN	JMP mconr

CONOUT	JMP mconw

LIST	RTS

LISTST	RTS

AUXOUT	RTS

AUXIN	RTS

HOME	CLC
	RTS
	
MONITOR	JMP mwrm

; a, b, c, d 320k   -  e, f, g  4M 

SELDSK	CMP #4			;TEST FOR DRIVE A to D
	BCS SELDS1
	STA DRIVE		;SELECT DISK LOGICAL
	LDA #<DPH320K		;SET DPHVEC
	STA DPHV
	LDA #>DPH320K
	STA DPHV+1
	CLC
	RTS

SELDS1	CMP #7			;TEST FOR ILEGAL DRIVE
	BCS SELDSER
	STA DRIVE
	LDA #<DPH4M		;SET DPHVEC
	STA DPHV
	LDA #>DPH4M
	STA DPHV+1
	CLC
	RTS

SELDSER	LDA #$F9		;ILLEGAL DRIVE NR
	SEC
	RTS


SETSEC	TAX			;sector pointer for p.00 in Accu
	LDA 0,X
	STA SECTOR
	LDA 1,X
	STA SECTOR+1
	LDA 2,X
	STA SECTOR+2
	RTS


SETTRK	LDA #$FB	;Sector not found
	SEC
	RTS

RWCOM	;lda SECTOR+2
	;jsr douta
	LDX DRIVE
	CPX #7		;CHECK FOR DRIVE > D
	BCS RWCOMQ
	LDA DSKOFS,X
	ORA SECTOR+1
	;jsr douta
	TAX
	LDA SECTOR
	;jsr douta
	;jsr mputcl	;mon print crlf
	JSR mcfll	;saves LBA0 and LBA1
	LDA SECTOR+2
	LDX #0
	JSR mcflh	;saves LBA2 and LBA3
	CLC
RWCOMQ	RTS

;douta	pha
;	jsr mputb	;mon print byte
;	pla
;	rts

READ	JSR RWCOM
	BCS SELDSER 
	LDA #<BUF512
	LDX #>BUF512
	JSR mcfr	; reads CF sector in LBA
	BNE READER
	LDY #0

READL	LDA BUF512,Y	;buffer used by CF read sector
	STA (DMAV),Y
	INY
	BNE READL
	CLC
	RTS

READER	LDA #$F7
	SEC
	RTS


WRITE	JSR RWCOM
	BCS SELDSER
	LDY #0
WRITEL	LDA (DMAV),Y
	STA BUF512,Y
	INY
	BNE WRITEL
	LDA #<BUF512
	LDX #>BUF512
	JSR mcfw
	BNE READER
	CLC
	RTS

GET_VERSION			;GET VERSION NUMBER
	LDA #VERSION
	RTS

BUF512	.dsb 512,0

DMA	.dsb 256,0

DIRBF	.dsb 256,0

BIOSEND
